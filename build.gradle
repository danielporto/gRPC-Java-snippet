// using gradle 6.7.1

plugins {
    // Provide convenience executables for trying out the examples.
    id 'application'
    // ASSUMES GRADLE 5.6 OR HIGHER. Use plugin version 0.8.10 with earlier gradle versions
    id 'com.google.protobuf' version '0.8.16'
    // Generate IntelliJ IDEA's .idea & .iml project files
    id 'idea'
    // java stuff
    id 'java'
    //id 'checkstyle'
    //id 'findbugs'
    //id 'pmd'
}

repositories {
    maven { // The google mirror is less flaky than mavenCentral()
        url "https://maven-central.storage-download.googleapis.com/maven2/" }
    mavenCentral()
    mavenLocal()
}
sourceCompatibility = 1.8
targetCompatibility = 1.8

// updating the version in our release process.
def grpcVersion = '1.39.0' // CURRENT_GRPC_VERSION
def protobufVersion = '3.17.2'
def protocVersion = protobufVersion

project.version ='1.1'
description = 'A simple grpc example with gradle'

dependencies {
    implementation "io.grpc:grpc-protobuf:${grpcVersion}"
    implementation "io.grpc:grpc-stub:${grpcVersion}"
    compileOnly "org.apache.tomcat:annotations-api:6.0.53"

    // examples/advanced need this for JsonFormat
    implementation "com.google.protobuf:protobuf-java-util:${protobufVersion}"

    runtimeOnly "io.grpc:grpc-netty-shaded:${grpcVersion}"

    testImplementation "io.grpc:grpc-testing:${grpcVersion}"
    testImplementation "junit:junit:4.12"
    testImplementation "org.mockito:mockito-core:3.4.0"
}


protobuf {
  protoc { artifact = "com.google.protobuf:protoc:${protocVersion}" }
  plugins {
        grpc { artifact = "io.grpc:protoc-gen-grpc-java:${grpcVersion}" }
    }

  generateProtoTasks.generatedFilesBaseDir = 'src-gen'

  generateProtoTasks {
    all()*.plugins {
      grpc {}
    }
  } //generateProtoTasks
}// protobuf

// Inform IDEs like IntelliJ IDEA, Eclipse or NetBeans about the generated code.
sourceSets {
    main {
        java {
            // include self written and generated code
            srcDirs = [ 'src/main/java', 'src-gen/main/grpc', 'src-gen/main/java' ]
        }
        proto {
            srcDirs = [ 'src/main/proto' ]
        }
    }
    // in future add test configuration here
}

startScripts.enabled = false

jar {
    manifest {
        attributes 'Implementation-Title': description,
                   'Implementation-Version': project.version
    }
}


//delete generated source code when clean
clean {
    delete "src-gen"
}


task helloServer(type: CreateStartScripts) {
    mainClassName = 'com.example.grpc.server.MyGreetingServer'
    applicationName = 'hello-world-server'
    outputDir = new File(project.buildDir, 'tmp')
    classpath = startScripts.classpath
}

task helloClient(type: CreateStartScripts) {
    mainClassName = 'com.example.grpc.client.MyBlockingGreetingClient'
    applicationName = 'hello-world-client'
    outputDir = new File(project.buildDir, 'tmp')
    classpath = startScripts.classpath
}

applicationDistribution.into('bin') {
    from(helloServer)
    // from(helloWorldClient)
    fileMode = 0755
}